plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'java'
    id "com.moowork.node" version "1.3.1"
}

apply plugin: 'io.spring.dependency-management'

group = 'com.food'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

node {
    download = true

    // nodeJS 위치
    workDir = file("${project.buildDir}/nodejs")

    // npm 위치
    npmWorkDir = file("${project.buildDir}/npm")
}

task installNpmApp(type: NpmTask) {
    description = "Installs all dependencies from package.json"
    workingDir = file("${project.projectDir}/front-web")
    args = ["install"]
}

task buildNpmApp(type: NpmTask) {
    description = "Builds production version of the webapp"
    workingDir = file("${project.projectDir}/front-web")
    args = ["run", "build"]
}

task copyWebApp(type: Copy) {
    from 'front-web/build'
    into 'build/resources/main/static/.'
}

// npm 설치, 빌드, copy에 대한 Build Script
buildNpmApp.dependsOn installNpmApp
copyWebApp.dependsOn buildNpmApp
compileJava.dependsOn copyWebApp


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compile ('com.h2database:h2')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Spring-boot-Security
    compile("org.springframework.boot:spring-boot-starter-security")

    // Swagger2
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'

    // Jasypt
    compile group: 'com.github.ulisesbocchio', name: 'jasypt-spring-boot-starter', version: '2.1.0'

}